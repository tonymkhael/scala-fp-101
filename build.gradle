buildscript {
    repositories {
        jcenter()
    }

    dependencies {
        classpath 'org.asciidoctor:asciidoctorj-pdf:1.5.0-alpha.8'
        classpath 'org.ysb33r.gradle:vfs-gradle-plugin:1.0-beta8'
        classpath 'commons-httpclient:commons-httpclient:3.1'
    }
}

plugins {
	id 'org.asciidoctor.convert' version '1.5.3'
	id 'com.github.jruby-gradle.base' version '1.2.1'
}

project.version = '1.0-SNAPSHOT'
project.group = 'org.tmkhael'
project.description = 'scala-fp-101'

asciidoctorj {
    version = '1.5.4'
}

ext {
    revealjsVersion = '3.2.0'
    asciidoctorBackendVersion = 'master'
    downloadDir = new File(buildDir,'download')
    templateDir = new File(downloadDir,'templates')
    revealjsDir   = new File(downloadDir,'reveal.js')
}

task download << {
    mkdir downloadDir
    vfs {
        cp "zip:https://github.com/asciidoctor/asciidoctor-reveal.js/archive/${asciidoctorBackendVersion}.zip!asciidoctor-reveal.js-${asciidoctorBackendVersion}",
                templateDir, recursive:true, overwrite:true
        cp "zip:https://github.com/hakimel/reveal.js/archive/${revealjsVersion}.zip!reveal.js-${revealjsVersion}",
                revealjsDir, recursive:true, overwrite:true
    }
}

download {
    description "Download extra revealjs resources"
    outputs.dir templateDir
    outputs.dir revealjsDir
}

apply plugin: 'com.github.jruby-gradle.base'
apply plugin: 'org.ysb33r.vfs'
apply plugin: 'java'
apply plugin: 'org.asciidoctor.convert'
apply plugin: 'scala'

dependencies {
    gems 'rubygems:haml:4.0.6'
    gems 'rubygems:slim:3.0.6'
    gems 'rubygems:thread_safe:0.3.5'
    compile 'org.scala-lang:scala-library:2.12.2'
    testCompile 'org.scalatest:scalatest_2.12:3.0.1'
    testCompile 'org.scalactic:scalactic_2.12:3.0.1'
    testCompile 'org.scalacheck:scalacheck_2.12:1.13.5'
    testCompile 'junit:junit:4.12'
}

test {

    beforeTest { descriptor ->
        logger.lifecycle("Running test: $descriptor")
    }

}

asciidoctor {
    dependsOn jrubyPrepare, download
    
    resources {
        from (sourceDir) {
            include 'images/**'
        }
        from (downloadDir) {
            include 'reveal.js/**'
        }
    }
    backends 'revealjs'
    attributes \
        'build-gradle': file('build.gradle'),
        'sourcedir': project.sourceSets.main.java.srcDirs[0],
        'endpoint-url': 'http://example.org',
        'source-highlighter': 'coderay',
        'imagesdir': './images',
        'toc': 'left',
        'icons': 'font',
        'setanchors': '',
        'idprefix': 'slide-',
        'idseparator': '-',
        'docinfo1': '',
        'revealjs_theme': 'black',
        'revealjs_transition': 'linear',
        'revealjs_history': 'true',
        'revealjs_slideNumber': 'true'
    options template_dirs: [new File(templateDir,'templates/slim').absolutePath]
}